plugins {
	id 'java-gradle-plugin'
	id 'jvm-test-suite'
	id 'dev.nokee.samples.generators'
	id 'dev.nokee.samples.copy-source'
	id 'nokeebuild.publishing'
	id 'nokeebuild.signing'
	id 'nokeebuild.test-kit-directory-in-test-suites'
	id 'nokeebuild.use-latest-java-lts-in-test-suites'
	id 'nokeebuild.use-junit-platform-in-test-suites'
	id 'nokeebuild.code-templates'
}

group = 'dev.nokee'
version = '1.0-milestone-27'
description = 'Quality-of-life enhancements for Gradle core native plugins.'

repositories {
	mavenLocal()
	mavenCentral()
	gradlePluginPortal()
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.named('javadoc', Javadoc) {
	options {
		windowTitle 'Nokee Companion'
		showFromPublic()

		addStringOption('Xdoclint:all,-missing')
	}
}

gradlePlugin {
	website = 'https://nokee.dev'
	vcsUrl = 'https://github.com/nokeedev/nokee-companion'
	plugins {
		legacy {
			id = 'dev.nokee.native-companion'
			implementationClass = 'dev.nokee.companion.LegacySupportPlugin'
			displayName = 'Nokee Native Companion'
			description = 'Provides quality-of-life enhancements for Gradle core native plugins.'
			tags = ['c++', 'cpp', 'native', 'gradle-native', 'cpp-library', 'cpp-application']
		}
	}

	dependencies {
		implementation libs.nokee.commons.names
		implementation libs.nokee.commons.backports
		implementation libs.nokee.commons.gradle

		implementation libs.apache.commons.io

		implementation libs.nokee.plugins.multiplatform.publishing
	}
}

testing {
	suites {
		test {
			dependencies {
				implementation 'dev.gradleplugins:gradle-api:latest.release'
				implementation libs.hamcrest.core
				implementation 'dev.nokee.commons:commons-hamcrest:latest.integration'
				implementation testFixtures('dev.nokee.commons:commons-gradle:latest.integration')
				implementation 'dev.nokee.templates:nokee-code-templates:latest.integration'
				implementation('com.google.guava:guava:33.4.6-jre') { because 'TypeToken' }
			}
		}
		functionalTest(JvmTestSuite) {
			dependencies {
				implementation gradleTestKit()
				implementation 'commons-io:commons-io:latest.release'
				implementation 'dev.nokee.commons:test-gradle-projects:latest.release'
				implementation 'dev.nokee.templates:nokee-code-templates:latest.integration'
				implementation 'dev.gradleplugins:gradle-runner-kit:latest.release'
				implementation 'org.jetbrains:annotations:26.0.2'
				implementation libs.hamcrest.core
				implementation 'dev.nokee.commons:commons-names:latest.release'
				implementation testFixtures('dev.nokee.commons:commons-gradle:latest.integration')
				implementation testFixtures('dev.gradleplugins:gradle-runner-kit:latest.release')
				implementation 'dev.nokee.commons:commons-hamcrest:latest.integration'
			}
		}
		samplesTest(JvmTestSuite) {
			targets.all {
				testTask.configure {
					samples.samples.all {
						systemProperty it.name, it.sampleDir.get().asFile.absolutePath
					}
				}
			}

			dependencies {
				implementation gradleTestKit()
				implementation 'commons-io:commons-io:latest.release'
				implementation 'dev.gradleplugins.exemplar:commons-exemplar:latest.integration'
				implementation 'dev.nokee.commons:test-gradle-projects:latest.release'
				implementation libs.hamcrest.core
				implementation 'dev.gradleplugins:gradle-runner-kit:latest.release'
			}
		}
	}
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)
gradlePlugin.testSourceSets.add(sourceSets.samplesTest)


import dev.nokee.elements.core.GradleLayoutElement
import dev.nokee.platform.jni.fixtures.GreeterAppWithJniLibrary
import dev.nokee.templates.CppApp
import dev.nokee.templates.CppList
import dev.nokee.templates.CppMessage
import dev.nokee.templates.CppUtilities

import java.nio.file.FileSystems
import java.nio.file.FileSystem

import static dev.nokee.elements.core.ProjectElement.ofMain
import static dev.nokee.elements.nativebase.NativeSourceElement.ofElements

samples {
	samples.configureEach { sampleDir = file("samples/${it.name}") }
	samples.register('cpp-binary-task-extensions') {
//		source {
//			project('.').buildRoot()
//			project('app').fromTemplate(new CppApp())
//			project('app').fromTemplate(new CppMessage().asImplementation())
//			project('lib').fromTemplate(new CppUtilities().withDllExport())
//			project('lib').fromTemplate(new CppList().withDllExport())
//		}
		elements {
			new GradleLayoutElement(ofMain(ofElements(new CppApp(), new CppMessage().asImplementation()))).writeToDirectory(location.resolve('app'))
			new GradleLayoutElement(ofMain(ofElements(new CppUtilities(), new CppList()))).writeToDirectory(location.resolve('lib'))
		}
	}
	samples.register('cpp-compile-detects-source-file-relocation') {
//		source {
//			project('.').buildRoot()
//			project('app').fromTemplate(new CppApp())
//			project('app').fromTemplate(new CppMessage().asImplementation())
//			project('utilities').fromTemplate(new CppUtilities().withDllExport())
//			project('list').fromTemplate(new CppList().withDllExport())
//		}
//		elements {
//			new GradleLayoutElement(ofMain(ofElements(new CppApp(), new CppMessage().asImplementation()))).writeToDirectory(location.resolve('app'))
//			new GradleLayoutElement(ofMain(new CppUtilities())).writeToDirectory(location.resolve('utilities'))
//			new GradleLayoutElement(ofMain(new CppList())).writeToDirectory(location.resolve('list'))
//
////			// TODO: Maybe:
////			project('app').fromTemplate(new CppApp(), new CppMessage().asImplementation())
////			project('utilities').fromTemplate(new CppUtilities())
////			project('list').fromTemplate(new CppList())
//		}
	}
	samples.register('cpp-compile-incremental-after-failure') {

	}
	samples.register('cpp-compile-object-files-extension') {
//		source {
//			project('app').fromTemplate(new CppApp(), [new CppMessage(), new CppUtilities(), new CppList()]*.asImplementation())
//		}
		elements {
			new GradleLayoutElement(ofMain(ofElements(new CppApp(), new CppMessage().asImplementation(), new CppUtilities().asImplementation(), new CppList().asImplementation()))).writeToDirectory(location.resolve('app'))
		}
	}
	samples.register('cpp-compile-options-per-source-files') {
//		source {
//			project('app').fromTemplate(new CppApp())
//			project('app').fromTemplate(new CppMessage().asImplementation())
//			project('lib').fromTemplate(new CppUtilities(), new CppList())
//		}
		elements {
			new GradleLayoutElement(ofMain(ofElements(new CppApp(), new CppMessage().asImplementation()))).writeToDirectory(location.resolve('app'))
			new GradleLayoutElement(ofMain(ofElements(new CppUtilities(), new CppList()))).writeToDirectory(location.resolve('lib'))
		}
	}
	samples.register('cpp-dependencies-version-catalog-support') {

	}
	samples.register('cpp-dependency-on-generated-public-headers') {
		elements {
			new GradleLayoutElement(ofMain(new CppApp())).writeToDirectory(location.resolve('app'))
			ofElements(new CppMessage(), new CppList(), new CppUtilities()).with {
				new GradleLayoutElement(ofMain(it.withoutPublicHeaders())).writeToDirectory(location.resolve('lib'))
				URI uri = URI.create("jar:file:" + location.resolve('lib/cpp-api-headers.zip'))
				try (FileSystem zipfs = FileSystems.newFileSystem(uri, [create: 'true'])) {
					it.publicHeaders.writeToDirectory(zipfs.getPath("/"))
				}
			}
		}
	}
	samples.register('cpp-multiple-compile-tasks') {

	}
	samples.register('cpp-multiple-public-header-directories') {
		elements {
			new GradleLayoutElement(ofMain(new CppApp())).writeToDirectory(location.resolve('app'))
			new CppMessage().with {
				it.publicHeaders.writeToDirectory(location.resolve("includes/message"))
				new GradleLayoutElement(ofMain(it.withoutPublicHeaders())).writeToDirectory(location.resolve('message'))
			}
			new CppUtilities().with {
				it.publicHeaders.writeToDirectory(location.resolve("includes/utilities"))
				new GradleLayoutElement(ofMain(it.withoutPublicHeaders())).writeToDirectory(location.resolve('utilities'))
			}
			new CppList().with {
				it.publicHeaders.writeToDirectory(location.resolve('includes/list'))
				new GradleLayoutElement(ofMain(it.withoutPublicHeaders())).writeToDirectory(location.resolve('utilities'))
			}
		}
	}
	samples.register('cpp-source-patterns') {

	}
	samples.register('cpp-additional-publishing-attributes') {
//		source {
//			project('app').fromTemplate(new CppApp())
//			project('lib').fromTemplate(new CppMessage(), new CppUtilities(), new CppList())
//		}
		elements {
			new GradleLayoutElement(ofMain(new CppApp())).writeToDirectory(location.resolve('app'))
			new GradleLayoutElement(ofMain(ofElements(new CppMessage(), new CppUtilities(), new CppList()))).writeToDirectory(location.resolve('lib'))
		}
	}
	samples.register('cpp-additional-publishing-variants') {
		elements {
			def app = new GreeterAppWithJniLibrary('greeter')
			app.application.writeToProject(location.resolve('app'))
			app.library.writeToProject(location.resolve('lib'))
		}
	}
	samples.register('cpp-compiler-argument-providers') {
//		source {
//			project('.').fromTemplate(new CppApp(), [new CppMessage(), new CppUtilities(), new CppList()]*.asImplementation())
//		}
		elements {
			new GradleLayoutElement(ofMain(ofElements())).writeToDirectory(location)
		}
	}
	samples.register('cpp-with-c-sources')
	samples.register('cpp-with-assembly-sources')
	samples.register('cpp-gnu-coverage') {
//		source {
//			project('app').fromTemplate(new CppApp(), new CppMessage().asImplementation())
//			project('utils').fromTemplate(new CppUtilities())
//			project('list').fromTemplate(new CppList())
//		}
		elements {
			new GradleLayoutElement(ofMain(ofElements(new CppApp(), new CppMessage().asImplementation()))).writeToDirectory(location.resolve('app'))
			new GradleLayoutElement(ofMain(new CppUtilities())).writeToDirectory(location.resolve('utils'))
			new GradleLayoutElement(ofMain(new CppList())).writeToDirectory(location.resolve('list'))
		}
	}
	samples.register('cpp-unit-test-tested-component-integration')

	samples.register('cpp-overlinking-avoidance')
}
