buildscript {
	dependencies {
		classpath 'dev.nokee.templates:nokee-code-templates:latest.integration'
	}
	repositories {
		mavenLocal()
	}
}

plugins {
	id 'java-gradle-plugin'
	id 'jvm-test-suite'
	id 'dev.nokee.samples.generators'
	id 'dev.nokee.samples.copy-source'
	id 'nokeebuild.publishing'
	id 'nokeebuild.signing'
	id 'nokeebuild.use-latest-java-lts-in-test-suites'
	id 'nokeebuild.use-junit-platform-in-test-suites'
}

group = 'dev.nokee'
version = '1.0-milestone-6'
description = 'Quality-of-life enhancements for Gradle core native plugins.'

repositories {
	mavenCentral()
	gradlePluginPortal()
	mavenLocal()
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.named('javadoc', Javadoc) {
	options {
		windowTitle 'Nokee Companion'
		showFromPublic()

		addStringOption('Xdoclint:all,-missing')
	}
}

gradlePlugin {
	website = 'https://nokee.dev'
	vcsUrl = 'https://github.com/nokeedev/nokee-companion'
	plugins {
		legacy {
			id = 'dev.nokee.native-companion'
			implementationClass = 'dev.nokee.companion.LegacySupportPlugin'
			displayName = 'Nokee Native Companion'
			description = 'Provides quality-of-life enhancements for Gradle core native plugins.'
			tags = ['c++', 'cpp', 'native', 'gradle-native', 'cpp-library', 'cpp-application']
		}
	}

	dependencies {
		implementation libs.nokee.commons.names
		implementation libs.nokee.commons.backports
		implementation libs.nokee.commons.gradle

		implementation libs.apache.commons.io

		implementation libs.nokee.plugins.multiplatform.publishing
	}
}

testing {
	suites {
		test {
			dependencies {
				implementation 'dev.gradleplugins:gradle-api:latest.release'
				implementation libs.hamcrest.core
				implementation 'dev.nokee.commons:commons-hamcrest:latest.integration'
				implementation testFixtures('dev.nokee.commons:commons-gradle:latest.release')
				implementation 'dev.nokee.templates:nokee-code-templates:latest.integration'
			}
		}
		functionalTest(JvmTestSuite) {
			dependencies {
				implementation gradleTestKit()
				implementation 'commons-io:commons-io:latest.release'
				implementation 'dev.nokee.commons:test-gradle-projects:latest.release'
				implementation 'dev.nokee.templates:nokee-code-templates:latest.integration'
				implementation 'dev.gradleplugins:gradle-runner-kit:latest.release'
				implementation 'org.jetbrains:annotations:26.0.2'
				implementation libs.hamcrest.core
				implementation 'dev.nokee.commons:commons-names:latest.release'
				implementation testFixtures('dev.nokee.commons:commons-gradle:latest.release')
				implementation testFixtures('dev.gradleplugins:gradle-runner-kit:latest.release')
				implementation 'dev.nokee.commons:commons-hamcrest:latest.integration'
			}
		}
		samplesTest(JvmTestSuite) {
			targets.all {
				testTask.configure {
					samples.samples.all {
						systemProperty it.name, it.sampleDir.get().asFile.absolutePath
					}
				}
			}

			dependencies {
				implementation gradleTestKit()
				implementation 'commons-io:commons-io:latest.release'
				implementation 'dev.gradleplugins.exemplar:commons-exemplar:latest.integration'
				implementation 'dev.nokee.commons:test-gradle-projects:latest.release'
				implementation libs.hamcrest.core
				implementation 'dev.gradleplugins:gradle-runner-kit:latest.release'
			}
		}
	}
}

gradlePlugin.testSourceSets.add(sourceSets.functionalTest)
gradlePlugin.testSourceSets.add(sourceSets.samplesTest)


import dev.gradleplugins.fixtures.sources.NativeElements
import dev.nokee.docs.samples.generators.copysource.CppLibraryTemplate
import dev.nokee.docs.samples.generators.copysource.SourceCopyTask
import dev.nokee.platform.jni.fixtures.GreeterAppWithJniLibrary
import dev.nokee.templates.CppApp

project.getTasks().withType(SourceCopyTask).configureEach {
	templatesDir = file('../../nokee-samples/native-samples/samples-dev/src/templates')
}


import java.nio.file.FileSystem
import java.nio.file.FileSystems

def cppUtilsLib = CppLibraryTemplate.of("cpp-lib-with-api-dep", "utilities")
def cppListLib = CppLibraryTemplate.of("cpp-lib", "list")
def cppMessageLib = CppLibraryTemplate.of("cpp-message-api", "message")


samples {
	samples.configureEach { sampleDir = file("samples/${it.name}") }
	samples.register('cpp-binary-task-extensions') {
		copySource {
			project(".").buildRoot()
			appProject("app").fromTemplate("cpp-app")
			appProject("app").fromTemplate(cppMessageLib)
			appProject("app").fromTemplate("cpp-message-static")
			libProject("lib").fromTemplate(cppUtilsLib)
			libProject("lib").fromTemplate(cppListLib)
		}
	}
	samples.register('cpp-compile-detects-source-file-relocation') {
//		copySource {
//			project(".").buildRoot()
//			appProject("app").fromTemplate("cpp-app")
//			appProject("app").fromTemplate(cppMessageLib)
//			appProject("app").fromTemplate("cpp-message-static")
//			libProject("utilities").fromTemplate(cppUtilsLib)
//			libProject("list").fromTemplate(cppListLib)
//		}
	}
	samples.register('cpp-compile-incremental-after-failure') {

	}
	samples.register('cpp-compile-object-files-extension') {
		copySource {
			project(".").buildRoot()
			appProject("app").fromTemplate("cpp-app")
			appProject("app").fromTemplate(cppMessageLib)
			appProject("app").fromTemplate("cpp-message-static")
			appProject("app").fromTemplate(cppUtilsLib)
			appProject("app").fromTemplate(cppListLib)
		}
	}
	samples.register('cpp-compile-options-per-source-files') {
		copySource {
			project(".").buildRoot()
			appProject("app").fromTemplate("cpp-app")
			appProject("app").fromTemplate(cppMessageLib)
			appProject("app").fromTemplate("cpp-message-static")
			libProject("lib").fromTemplate(cppUtilsLib)
			libProject("lib").fromTemplate(cppListLib)
		}
	}
	samples.register('cpp-dependencies-version-catalog-support') {

	}
	samples.register('cpp-dependency-on-generated-public-headers') {
		elements {
			def app = new CppApp()
			app.withoutImplementation().writeToProject(location.resolve('app'))
			app.getLibs().with {
				it.get(NativeElements::privateHeaders).writeToProject(location.resolve('lib'))
				it.get(NativeElements::sources).writeToProject(location.resolve('lib'))

				URI uri = URI.create("jar:file:" + location.resolve('lib/cpp-api-headers.zip'))
				try (FileSystem zipfs = FileSystems.newFileSystem(uri, [create: 'true'])) {
					it.get(NativeElements::publicHeaders).writeToSourceDir(zipfs.getPath("/"))
				}
			}
		}
	}
	// TODO: Rename this sample
	samples.register('cpp-header-dependencies-normalization') {
//		copySource {
//			project(".").buildRoot()
//			appProject("app").fromTemplate("cpp-app")
//			appProject("app").fromTemplate(cppMessageLib)
//			appProject("app").fromTemplate("cpp-message-static")
//			libProject("lib").fromTemplate(cppUtilsLib)
//			libProject("lib").fromTemplate(cppListLib)
//		}
	}
	samples.register('cpp-multiple-compile-tasks') {

	}
	samples.register('cpp-multiple-public-header-directories') {
		elements {
			def app = new CppApp()
			app.withoutImplementation().writeToProject(location.resolve('app'))
			app.getMessage().asLib().with {
				it.get(NativeElements::publicHeaders).writeToSourceDir(location.resolve('includes/message'))
				it.get(NativeElements::privateHeaders).writeToProject(location.resolve('message'))
				it.get(NativeElements::sources).writeToProject(location.resolve('message'))
			}
			app.getUtilities().asLib().with {
				it.get(NativeElements::publicHeaders).writeToSourceDir(location.resolve('includes/utilities'))
				it.get(NativeElements::privateHeaders).writeToProject(location.resolve('utilities'))
				it.get(NativeElements::sources).writeToProject(location.resolve('utilities'))
			}
			app.getList().asLib().with {
				it.get(NativeElements::publicHeaders).writeToSourceDir(location.resolve('includes/list'))
				it.get(NativeElements::privateHeaders).writeToProject(location.resolve('utilities'))
				it.get(NativeElements::sources).writeToProject(location.resolve('utilities'))
			}
		}
	}
	samples.register('cpp-source-patterns') {

	}
	samples.register('cpp-additional-publishing-attributes') {
		elements {
			def app = new CppApp()
			app.withoutImplementation().writeToProject(location.resolve('app'))
			app.libs.writeToProject(location.resolve('lib'))
		}
	}
	samples.register('cpp-additional-publishing-variants') {
		elements {
			def app = new GreeterAppWithJniLibrary('greeter')
			app.application.writeToProject(location.resolve('app'))
			app.library.writeToProject(location.resolve('lib'))
		}
	}
	samples.register('cpp-compiler-argument-providers') {
		elements {
			def app = new CppApp()
			app.writeToProject(location)
		}
	}
}
